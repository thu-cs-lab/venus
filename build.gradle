group 'venus'
version '0.1.0'

buildscript {
    ext.kotlin_version = '1.1.4'
    ext.dokka_version = '0.9.14'

    repositories {
        maven { url 'https://dl.bintray.com/kotlin/kotlin-1.1.4' }
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.dokka'

runDceKotlinJs.keep 'venus_main.venus.glue.Driver.run', 'venus_main.venus.assembler.Linter'

configurations {
    ktlint
}

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlin-1.1.4' }
    mavenCentral()
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "--verbose", "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

task gruntTests(type: Exec) {
    commandLine 'grunt', 'test'
}

test.dependsOn gruntTests

dokka {
    outputFormat = 'html'
    outputDirectory = "doc"
}

dependencies {
    ktlint 'com.github.shyiko:ktlint:0.8.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}
